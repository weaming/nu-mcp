{
  description = "Rust Nushell MCP Server";

  inputs = {
    base-nixpkgs.url = "github:ck3mp3r/flakes?dir=base-nixpkgs";
    nixpkgs.follows = "base-nixpkgs/unstable";
    flake-parts = {
      url = "github:hercules-ci/flake-parts";
      inputs.nixpkgs-lib.follows = "nixpkgs";
    };
    rustnix = {
      url = "github:ck3mp3r/flakes?dir=rustnix";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    topiary-nu = {
      url = "github:ck3mp3r/flakes?dir=topiary-nu";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    nu-mods = {
      url = "github:ck3mp3r/nu-mods";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs = inputs @ {
    self,
    flake-parts,
    ...
  }:
    flake-parts.lib.mkFlake {inherit inputs;} {
      systems = ["aarch64-darwin" "aarch64-linux" "x86_64-darwin" "x86_64-linux"];
      perSystem = {
        config,
        system,
        ...
      }: let
        supportedTargets = ["aarch64-darwin" "aarch64-linux" "x86_64-darwin" "x86_64-linux"];
        overlays = [
          inputs.topiary-nu.overlays.default
          inputs.base-nixpkgs.overlays.default
        ];
        pkgs = import inputs.nixpkgs {
          localSystem = system;
          inherit overlays;
        };

        cargoToml = fromTOML (builtins.readFile ./Cargo.toml);
        cargoLock = {lockFile = ./Cargo.lock;};

        # Helper function to create tool packages
        mkToolPackage = {
          pname,
          src,
          installPath,
          description,
          buildInputs ? [],
          nativeBuildInputs ? [],
          propagatedBuildInputs ? [],
        }:
          pkgs.stdenv.mkDerivation {
            inherit pname src buildInputs nativeBuildInputs propagatedBuildInputs;
            version = cargoToml.package.version;

            dontBuild = true;
            dontConfigure = true;

            installPhase = ''
              runHook preInstall

              mkdir -p $out/share/nushell/mcp-tools/${installPath}
              cp -r * $out/share/nushell/mcp-tools/${installPath}/

              # Copy shared _common library from buildInputs if present
              for dep in $buildInputs; do
                if [ -d "$dep/share/nushell/mcp-tools/_common" ]; then
                  mkdir -p $out/share/nushell/mcp-tools/_common
                  cp -r $dep/share/nushell/mcp-tools/_common/* $out/share/nushell/mcp-tools/_common/
                fi
              done

              runHook postInstall
            '';

            # Ensure propagated dependencies are properly handled
            passthru = {
              # Make dependencies easily accessible for debugging
              runtimeDependencies = propagatedBuildInputs;
            };

            meta = with pkgs.lib; {
              inherit description;
              license = licenses.mit;
              platforms = platforms.all;
            };
          };

        # Install data for pre-built releases
        installData = {
          aarch64-darwin =
            if builtins.pathExists ./data/aarch64-darwin.json
            then builtins.fromJSON (builtins.readFile ./data/aarch64-darwin.json)
            else {};
          aarch64-linux =
            if builtins.pathExists ./data/aarch64-linux.json
            then builtins.fromJSON (builtins.readFile ./data/aarch64-linux.json)
            else {};
          x86_64-darwin =
            if builtins.pathExists ./data/x86_64-darwin.json
            then builtins.fromJSON (builtins.readFile ./data/x86_64-darwin.json)
            else {};
          x86_64-linux =
            if builtins.pathExists ./data/x86_64-linux.json
            then builtins.fromJSON (builtins.readFile ./data/x86_64-linux.json)
            else {};
        };

        # Build regular packages (no archives)
        regularPackages = inputs.rustnix.lib.rust.buildTargetOutputs {
          inherit
            cargoToml
            cargoLock
            overlays
            pkgs
            system
            installData
            supportedTargets
            ;
          nixpkgs = inputs.nixpkgs;
          src = ./.;
          packageName = "nu-mcp";
          archiveAndHash = false;
          nativeBuildInputs = [pkgs.nushell];
        };

        # Build archive packages (creates archive with system name)
        archivePackages = inputs.rustnix.lib.rust.buildTargetOutputs {
          inherit
            cargoToml
            cargoLock
            overlays
            pkgs
            system
            installData
            supportedTargets
            ;
          nixpkgs = inputs.nixpkgs;
          src = ./.;
          packageName = "archive";
          archiveAndHash = true;
          nativeBuildInputs = [pkgs.nushell];
        };
      in {
        apps = {
          default = {
            type = "app";
            program = "${config.packages.default}/bin/nu-mcp";
          };
        };

        packages = let
          toolPackages = import ./nix/packages.nix {
            inherit pkgs cargoToml mkToolPackage;
          };

          # Container image - only on Linux (cross-compilation deferred to CI)
          containerPackage =
            if builtins.match ".*-darwin" system == null
            then {
              container = import ./nix/container.nix {
                inherit pkgs cargoToml;
                defaultPackage = config.packages.default;
              };
            }
            else {};
        in
          regularPackages
          // archivePackages
          // toolPackages
          // containerPackage;

        devShells = {
          # Regular shell for development - loaded from devenv.nix module
          default = import ./nix/dev.nix {
            inherit inputs pkgs system;
          };

          # Classic shell for CI - just toolchains, no devenv
          ci = import ./nix/ci.nix {
            inherit pkgs inputs system;
          };
        };

        formatter = pkgs.alejandra;
      };

      flake = {
        overlays.default = final: prev: {
          nu-mcp = self.packages.default;
        };
      };
    };
}
